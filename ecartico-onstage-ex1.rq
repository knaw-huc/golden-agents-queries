#+ summary: Question 1: What is the age of playwriters when their work is first performed?
#+ endpoint: https://sparql.diginfra.net/sparql
#+ tags:
#+  - ecartico
#+  - onstage
#+  - linkset

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# What is the age of playwriters when their work is first performed?
# Using: ECARTICO + ONSTAGE
# 2020-04-29
# Expected rows: 10

SELECT DISTINCT ?name ?birthYear (min(?performanceDate) as ?firstPerformance) ((year(?firstPerformance) - ?birthYear) as ?age) ?deathYear
WHERE {
  GRAPH <https://data.goldenagents.org/datasets/ufab7d657a250e3461361c982ce9b38f3816e0c4b/ecartico_20200316/> {
    
    # Biographic info
		?personEcartico a schema:Person ;
  			schema:name ?name ;
            schema:birthDate ?birthDate ;
            schema:deathDate ?deathDate .
    
    # Some birthDate and deathDate values are resources (schema:StructuredValue). Filter those out.
        FILTER (!ISURI(?birthDate) && !ISURI(?deathDate))
        FILTER (DATATYPE(?birthDate) = xsd:date)  # please, format all dates in xsd:date...
        FILTER (DATATYPE(?deathDate) = xsd:date)  # please, format all dates in xsd:date...
    
    # Some birthDate and deathDate values are of type xsd:gYear. Cast them to integer.
      BIND(IF(DATATYPE(?birthDate) = xsd:date, year(?birthDate), xsd:integer(substr(str(?birthDate), 0, 4))) as ?birthYear)
      BIND(IF(DATATYPE(?deathDate) = xsd:date, year(?deathDate), xsd:integer(substr(str(?deathDate), 0, 4))) as ?deathYear)
	}

  GRAPH <https://data.goldenagents.org/datasets/ufab7d657a250e3461361c982ce9b38f3816e0c4b/onstage_20200316/> {
		?personOnstage a schema:Person .
    
    # A work is written by a person. 
    	?work a schema:CreativeWork ;
                schema:creator ?personOnstage .
    
    # A performance is played on a date. The play is based on the work.
    	?show a schema:Event ;
                schema:startDate ?performanceDate ;
           schema:subEvent*/schema:workPerformed ?work .
    
    # Some birthDate and deathDate values are of type xsd:gYear. Cast them to integer.
	    BIND(IF(DATATYPE(?performanceDate) = xsd:gYear, xsd:integer(str(?performanceDate)), ?performanceDate) as ?performanceYear)
    
  }

  GRAPH ?linkset {
    {
      ?personOnstage ?link ?personEcartico
    } UNION {
      ?personEcartico ?link ?personOnstage
    }
  }
  
  FILTER(?linkset = <https://data.goldenagents.org/datasets/ufab7d657a250e3461361c982ce9b38f3816e0c4b/linkset_ecartico_onstage_sameas_including_external_links_20190218/> ||
         ?linkset = <https://data.goldenagents.org/datasets/ufab7d657a250e3461361c982ce9b38f3816e0c4b/linkset_onstage_ecartico_sameas_20190218/> )
  
} 

GROUP BY ?personEcartico ?name ?birthYear ?deathYear ?deathDate ?firstPerformance
HAVING(min(?performanceDate) < ?deathDate)
ORDER BY ?name
