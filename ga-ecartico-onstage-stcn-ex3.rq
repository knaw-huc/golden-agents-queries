#+ summary: How many plays were made and performed during a playwriter's life? How many works of this author are listed in the STCN? And how many unique printer/publishers are involved in the publication? In Golden Agents ontology.
#+ endpoint: https://sparql.goldenagents.org/
#+ method: GET
#+ tags:
#+  - ecartico
#+  - onstage
#+  - stcn
#+  - linkset

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ga: <https://data.goldenagents.org/ontology/>

# How many plays were made and performed during a playwriter's life? How many works of this author are listed in the STCN? And how many unique printer/publishers are involved in the publication?
# Using: ECARTICO + ONSTAGE + STCN through Golden Agents ontology
# 2020-12-16
# Expected rows: 116 (with multiple names and dates, execution time around 10 minutes)

SELECT DISTINCT 
    (SAMPLE(?name) AS ?name)
    (SAMPLE(?birthYear) AS ?birthYear)
    (min(?performanceDate) as ?firstPerformanceAlive) 
    (max(?performanceDate) as ?lastPerformanceAlive) 
    (SAMPLE(?deathYear) AS ?deathYear)
    (COUNT(DISTINCT ?work) as ?works) 
    (COUNT(DISTINCT ?show) as ?shows)
    (COUNT(DISTINCT ?performingEvent) as ?performances)
    (COUNT(DISTINCT ?otherWork) as ?otherWorks)
    (COUNT(DISTINCT ?printerSTCN) as ?uniquePrinters)
WHERE {
    ?person a ga:CreativeAgent ;
            ga:hasName ?name ;
            ga:birthDate ?birthDate ;
            ga:deathDate ?deathDate .

    # Some birthDate and deathDate values are of type xsd:gYear. Cast them to integer.
      BIND(IF(DATATYPE(?birthDate) = xsd:date, year(?birthDate), xsd:integer(substr(str(?birthDate), 1, 4))) as ?birthYear)
      BIND(IF(DATATYPE(?deathDate) = xsd:date, year(?deathDate), xsd:integer(substr(str(?deathDate), 1, 4))) as ?deathYear)
        # BIND(YEAR(?birthDate) AS ?birthYear)
        # BIND(YEAR(?deathDate) AS ?deathYear)

    # A work is written by a person.
    ?person ga:authorOf ?work .

    # A work is performed.
      ?work a ga:Story ;
              ga:performedAs ?performingEvent .

      ?performingEvent ga:subEventOf  ?show .

    # A performance is played on a date.
      ?show a ga:CreativeAct ;
              ga:startDate ?performanceDate .

    # Some birthDate and deathDate values are resources (schema:StructuredValue). Filter those out.
      FILTER (!ISURI(?birthDate) && !ISURI(?deathDate))

    # Only performances during a person's life
      FILTER(year(?performanceDate) <= ?deathYear)

    # Added for the STCN
      ?allBook ga:writtenBy ?person ;
               ga:publishedBy ?printerSTCN .

}
GROUP BY ?person 
ORDER BY ?name