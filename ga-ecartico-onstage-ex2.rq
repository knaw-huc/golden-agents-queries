#+ summary: How many plays were made and performed during a playwriter's life? In Golden Agents ontology.
#+ endpoint: https://sparql.diginfra.net/sparql
#+ method: GET
#+ tags:
#+  - ecartico
#+  - onstage
#+  - linkset

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ga: <https://data.goldenagents.org/ontology/>

# How many plays were made and performed during a playwriter's life?
# Using: ECARTICO + ONSTAGE through Golden Agents ontology
# 2020-03-20
# Expected rows: 13

SELECT DISTINCT ?name ?birthYear (min(?performanceDate) as ?firstPerformanceAlive) (max(?performanceDate) as ?lastPerformanceAlive) ?deathYear
	(COUNT(DISTINCT ?work) as ?works) (COUNT(DISTINCT ?show) as ?shows)
	(COUNT(DISTINCT ?performingEvent) as ?performances)
WHERE {
  	?person a ga:CreativeAgent .

  # {
  #   ?personOnstage		owl:sameAs*  ?personEcartico .
  # }	UNION {
  #   ?personEcartico		owl:sameAs*  ?personOnstage .
  # }

    # Biographic information
	?person ga:hasName ?name ;
                 ga:birthDate ?birthDate ;
                 ga:deathDate ?deathDate .

    # Some birthDate and deathDate values are of type xsd:gYear. Cast them to integer.
      BIND(IF(DATATYPE(?birthDate) = xsd:date, year(?birthDate), xsd:integer(substr(str(?birthDate), 0, 4))) as ?birthYear)
      BIND(IF(DATATYPE(?deathDate) = xsd:date, year(?deathDate), xsd:integer(substr(str(?deathDate), 0, 4))) as ?deathYear)

    # A work is written by a person.
		?person ga:authorOf ?work .

  	# A work is performed.
		?work a ga:Story ;
              ga:performedAs ?performingEvent .

  		?performingEvent ga:subEventOf*	?show .

	# A performance is played on a date.
  		?show a ga:CreativeAct ;
              ga:startDate ?performanceDate .

    # Some birthDate and deathDate values are resources (schema:StructuredValue). Filter those out.
        FILTER (!ISURI(?birthDate) && !ISURI(?deathDate))

    # Only performances during a person's life
		FILTER(year(?performanceDate) <= ?deathYear)

}
GROUP BY ?person ?name ?birthYear ?deathYear
ORDER BY ?name
